#!/bin/bash

grep -REh '^\s*(class|module|def|end)\b' lib | awk '
  function get_indent_level() {
    return match($0, "[^[:blank:]]") - 1
  }

  /^\s*(module|class)\>/ {
    match($0, /^\s*(class|module)\s*([^[:blank:]]+)/, matches)
    indent_level = get_indent_level()
    context[indent_level] = matches[2]
    for (i in context) {
      if (i > indent_level) {
        delete context[i]
      }
    }
  }

  /^\s*end/ {
    indent_level = get_indent_level()
    matches_one = 0
    for (i in context) {
      if (i == indent_level) {
        matches_one = 1
      }
    }
    if (matches_one) {
      for (i in context) {
        if (i >= indent_level) {
          delete context[i]
        }
      }
    }
  }

  /^\s*def\>/ {
    indent_level = get_indent_level()
    first = 1
    for (i in context) {
      if (i < indent_level) {
        printf(first ? "%s" : "::%s", context[i])
        first = 0
      }
    }
    match($0, /^\s*(def)\s*(self\.)?([^([:blank:]]+)/, matches)
    print (matches[2] ? "." : "#") matches[3]
  }
'
